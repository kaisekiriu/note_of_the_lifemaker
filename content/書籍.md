---
cssclasses: cards, cards-2-3, cards-cols-5
---

```dataview
TABLE WITHOUT ID
choice(
  typeof(thumbnail) = "array", 
  choice(
    contains(thumbnail[0], "http"), 
    "![anyName|50](" + thumbnail[0] + ")", 
      choice(
        typeof(link(thumbnail)) = "link", 
        embed(link(thumbnail[0])), 
        embed(link("Attached_File/bookcover_dummy.png"))
      )
  ), 
  choice(
    contains(thumbnail, "http"), 
    "![anyName|50](" + thumbnail + ")", 
      choice(
        typeof(link(thumbnail)) = "link", 
        embed(link(thumbnail)), 
        embed(link("Attached_File/bookcover_dummy.png"))
      )
  )
) as Thumbnail,
"[[" + file.name + "|"  + regexreplace(regexreplace(file.name, "^📕", ""), "^『(.+)』$", "$1") +  "]]" AS Title
from #書籍
```